apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'


version = '1.0.1'
group = 'net.saliman'
def artifact = 'groovy-liquibase-dsl'


configurations {
  deployment
  published.extendsFrom archives, signatures
}


repositories {
  mavenCentral()
}


dependencies {
  groovy 'org.codehaus.groovy:groovy:1.8.6'
  runtime 'org.codehaus.groovy:groovy:1.8.6'
  testCompile 'junit:junit:4.8.2'
  compile 'org.liquibase:liquibase-core:3.1.1'
  deployment "org.apache.maven.wagon:wagon-webdav-jackrabbit:1.0-beta-7"
  testRuntime 'com.h2database:h2:1.3.159'
}


jar {
  baseName = artifact
}


task sourceJar(type: Jar) {
  description = 'An archive of the source code for Maven Central'
  baseName = artifact
  classifier = 'sources'
  from sourceSets.main.groovy
}


task groovydocJar(type: Jar, dependsOn: groovydoc) {
  description = 'An archive of the GroovyDocs for Maven Central'
  baseName = artifact
  classifier = 'javadoc'
  from fileTree(groovydoc.destinationDir)
}

artifacts {
  archives groovydocJar, sourceJar
}

signing {
  sign configurations.archives
}


install {
  repositories.mavenInstaller {
    pom.project(pomConfiguration)
  }
}


uploadPublished {
  dependsOn << [ signArchives ]
  repositories.mavenDeployer {
    beforeDeployment { deployment ->
      signPom(deployment)
    }
    name = 'mavenCentralReleaseDeployer'
    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: mavenCentralUsername, password: mavenCentralPassword)
      releases(updatePolicy: 'always')
      snapshots(updatePolicy: 'never')
    }
    pom.project(pomConfiguration)
  }
}

//
// A single place to create the POM configuration, so it can be
// applied from the install and the upload tasks.
//
def getPomConfiguration() {
  return {
    name('Groovy Liquibase DSL')
    description('A Groovy-based DSL for the Liquibase database refactoring tool.')
    url('https://github.com/tlberglund/groovy-liquibase')
    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }
    developers {
      developer {
        id('tlberglund')
        name('Tim Berglund')
        email('tlberglund@gmail.com')
      }
      developer {
        id 'stevesaliman'
        name 'Steven C. Saliman'
        email 'support@saliman.net'
      }
    }
    scm {
      connection('scm:https://tlberglund@github.com/tlberglund/groovy-liquibase')
      developerConnection('scm:git@github.com:tlberglund/groovy-liquibase.git')
      url('https://github.com/tlberglund/groovy-liquibase')
    }
  }
}

